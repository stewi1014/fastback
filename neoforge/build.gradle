plugins {
    id "net.neoforged.moddev"
    id "com.gradleup.shadow"
}

neoForge {
    version = neo_version
    runs {
        server {
            server()
            programArguments = ["-nogui"]
        }
    }

    mods {
        fastback {
            sourceSet sourceSets.main
        }
    }
}

archivesBaseName = "${project.archives_base_name}"
version = "${project.mod_version}-neoforge"
group = project.maven_group

configurations {
    include
    additionalRuntimeClasspath.extendsFrom include
}

dependencies {
    include implementation("org.eclipse.jgit:org.eclipse.jgit:${project.jgit_version}") { transitive = false }
    include runtimeOnly("com.googlecode.javaewah:JavaEWAH:${project.JavaEWAH_version}") { transitive = false }
    include runtimeOnly("org.eclipse.jgit:org.eclipse.jgit.ssh.apache:${project.jgit_version}") { transitive = false }
    include runtimeOnly("org.apache.sshd:sshd-core:${project.apache_sshd_version}") { transitive = false }
    include runtimeOnly("org.apache.sshd:sshd-common:${project.apache_sshd_version}") { transitive = false }
    include runtimeOnly("net.i2p.crypto:eddsa:${project.eddsa_version}") { transitive = false }

    implementation project(path: ":common", configuration: "namedElements")
    include project(path: ":common", configuration: "namedElements")
}

shadowJar {
    zip64 = true
    configurations = [project.configurations.include]
    archiveClassifier = null
}

jar.enabled = false
tasks.build.dependsOn shadowJar

processResources {
    inputs.property "version", project.version
    filesMatching("META-INF/neoforge.mods.toml") {
        expand "version": project.version
    }
}
